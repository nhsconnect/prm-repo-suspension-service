plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
    id 'com.github.spotbugs' version '4.6.0'
    id 'org.owasp.dependencycheck' version '7.1.1'
}

group = 'uk.nhs.prm.repo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

//Without this task two jars are built, the additional "-plain.jar" is not needed
// for more details refer to: https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#packaging-executable.and-plain-archives
jar {
    enabled = false
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'com.github.spotbugs:spotbugs-annotations:3.1.3'
    implementation platform('software.amazon.awssdk:bom:2.17.73')
    implementation 'software.amazon.awssdk:cloudwatch:2.17.73'
    implementation 'software.amazon.awssdk:sns:2.17.73'
    implementation 'software.amazon.awssdk:sqs:2.17.73'
    implementation 'software.amazon.awssdk:dynamodb:2.17.130'
    // aws-java-sdk-core and sqs refs are to mitigate CVE-2022-31159.
    // Once we upgrade amazon-sqs-java-messaging-lib to v2 we can remove aws-java-sdk-core and sqs.
    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.1.0'
    implementation 'com.amazonaws:aws-java-sdk-core:1.12.285'
    implementation 'com.amazonaws:aws-java-sdk-sqs:1.12.285'

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.google.guava:guava:31.0.1-jre'

    implementation 'io.github.resilience4j:resilience4j-retry:1.7.1'
    implementation 'org.springframework:spring-jms:5.3.19'

    // force log4j related jars beyond log4shell vulnerability to ensure no false positives
    implementation 'org.apache.logging.log4j:log4j-api:2.17.2'
    implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.17.2'

    compileOnly 'org.projectlombok:lombok:1.18.20'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.4'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.awaitility:awaitility:3.0.0'
    testImplementation 'org.springframework.boot:spring-boot-test'
    testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-aop'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.33.2'

    // CVE-2022-25857 fix. We can remove it when springframework.boot and fasterxml libs
    // (which depend on it) cover this update
    implementation 'org.yaml:snakeyaml:1.33'

    // CVE-2020-36518...
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.1'
    // including workaround for bom mismatch which should be retired with full jackson-databind release
    components {
        withModule("com.fasterxml.jackson.core:jackson-databind") {
            allVariants {
                withDependencies {
                    removeAll {
                        it.name == "jackson-bom"
                    }
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}

test.outputs.upToDateWhen {false}

sourceSets {
    integration {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

tasks.withType(Test) {
    useJUnitPlatform()
}

task integration(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    mustRunAfter tasks.test
}

integration.outputs.upToDateWhen {false}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    shouldRunAfter integration
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            limit {
                minimum = 0.0
                //TODO: increase to to 0.7
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/model/**')
        }))
    }
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsTest {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsIntegration {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/integration/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

dependencyCheck {
    failBuildOnCVSS = 6
    suppressionFile = './dependency-checks-suppression.xml'
    analyzers {
          assemblyEnabled = false
    }

}

check.dependsOn integration
